/*
 * maven Gradle build script
 */

def localrepo = System.getProperty('maven.repo.local')
if (localrepo) {
  localrepo = relativePath(uri(gradle.startParameter.currentDir).resolve(localrepo))
}
def releaserepo = uri(bnd('releaserepo', rootProject.file('dist/bundles'))) /* Release repository. */

task deploy(type:Exec) {
  dependsOn ':dist:build'
  if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
    executable 'cmd'
    args '/c', rootProject.file('mvnw.cmd')
  } else {
    executable rootProject.file('mvnw')
  }
  args '--batch-mode'
  if (!logger.isInfoEnabled()) {
    args '--quiet'
  }
  args "-DaltDeploymentRepository=dist::default::${releaserepo}"
  if (localrepo) {
    args "-Dmaven.repo.local=${localrepo}"
  }
  args 'deploy'
}
