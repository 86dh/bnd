/*
 * maven Gradle build script
 */
import org.apache.tools.ant.taskdefs.condition.Os

var localrepo = System.getProperty("maven.repo.local")
if (localrepo) {
	localrepo = relativePath(uri(gradle.getStartParameter().getCurrentDir()).resolve(localrepo))
}
var dist = parent.project(bnd_build)
var windows = Os.isFamily(Os.FAMILY_WINDOWS)
var mvnw = rootProject.file(windows ? "mvnw.cmd" : "mvnw")

var deploy = tasks.register("deploy", Exec.class) {
	var releaserepo = uri(bnd.get("releaserepo", dist.file("bundles"))) /* Release repository. */
	dependsOn(dist.tasks.named("releaseDependencies"))
	if (windows) {
		executable = "cmd"
		args("/c", mvnw)
	} else {
		executable = mvnw
	}
	args("--batch-mode")
	args("--no-transfer-progress")
	if (logger.isDebugEnabled()) {
		args("--debug")
	}
	args("-Pdist")
	args("-Dreleaserepo=${releaserepo}")
	if (localrepo) {
		args("-Dmaven.repo.local=${localrepo}")
	}
	args("deploy")
}

var deployJFrog = tasks.register("deployJFrog", Exec.class) {
	enabled = !bnd.get("-releaserepo.jfrog", "").empty
	var settings = cnf.file("ext/jfrog-settings.xml")
	var deployState = deploy.map { it.state }
	onlyIf {
		var state = deployState.get()
		state.didWork && (state.failure == null)
	}
	dependsOn(deploy)
	if (windows) {
		executable = "cmd"
		args("/c", mvnw)
	} else {
		executable = mvnw
	}
	args("--batch-mode")
	args("--no-transfer-progress")
	if (logger.isDebugEnabled()) {
		args("--debug")
	}
	args("-Pjfrog")
	args("--settings=${settings}")
	if (localrepo) {
		args("-Dmaven.repo.local=${localrepo}")
	}
	args("deploy")
}

deploy.configure {
	finalizedBy(deployJFrog)
}

var mvnClean = tasks.register("mvnClean", Exec.class) {
	if (windows) {
		executable = "cmd"
		args("/c", mvnw)
	} else {
		executable = mvnw
	}
	args("--batch-mode")
	args("--no-transfer-progress")
	if (logger.isDebugEnabled()) {
		args("--debug")
	}
	if (localrepo) {
		args("-Dmaven.repo.local=${localrepo}")
	}
	args("clean")
}

tasks.named("clean") {
	dependsOn(mvnClean)
}

var mvnTestCompile = tasks.register("test-compile", Exec.class) {
	dependsOn(dist.tasks.named("jarDependencies"))
	if (windows) {
		executable = "cmd"
		args("/c", mvnw)
	} else {
		executable = mvnw
	}
	args("--batch-mode")
	args("--no-transfer-progress")
	if (logger.isDebugEnabled()) {
		args("--debug")
	}
	if (localrepo) {
		args("-Dmaven.repo.local=${localrepo}")
	}
	args("test-compile")
}
