/*
 *
 */

import aQute.bnd.gradle.Resolve
import aQute.bnd.gradle.TestOSGi

plugins {
	id 'biz.aQute.bnd.builder'
}

version = '1.0.0'
java {
	sourceCompatibility = '17'
	targetCompatibility = '17'
}

repositories {
	mavenLocal().metadataSources { mavenPom(); artifact() }
	mavenCentral()
}

dependencies {
	implementation "biz.aQute.bnd:biz.aQute.junit:${bnd_version}"
	runtimeOnly 'org.eclipse.platform:org.eclipse.osgi:3.18.100'
}

def resolveTask = tasks.register('resolve', Resolve) {
	description = 'Resolve testosgi.bndrun'
	group = 'test'
	inputs.files tasks.withType(Jar)
	bndrun = file('testosgi.bndrun')
	outputBndrun = layout.buildDirectory.file('testosgi.bndrun')
}

def testosgiTask = tasks.register('testosgi', TestOSGi) {
	description = 'OSGi Test testosgi.bndrun'
	group = 'test'
	bndrun = resolveTask.flatMap { it.outputBndrun }
	javaLauncher = javaToolchains.launcherFor {
		languageVersion = JavaLanguageVersion.of(JavaVersion.current().getMajorVersion())
	}
}

def checkTask = tasks.named('check') {
	dependsOn testosgiTask
}
